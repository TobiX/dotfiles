# Stuff for different applications

# kde madness ;)
if [[ -x =kioclient ]]
then
	alias k='kioclient exec .'
	alias kcp='kioclient copy'
	alias kmv='kioclient move'
elif [[ -x =kfmclient ]]
then
	alias k='kfmclient openURL .'
	alias kcp='kfmclient copy'
	alias kmv='kfmclient move'
fi

# devtodo
if [[ -x =devtodo ]]
then
	TODO_OPTIONS=(--timeout --summary)

	alias td='todo'          # For devtodo, two chars shorter :)
	alias tdl='todo --link'  # Debian...

	# Run on directory changes
	autoload -Uz add-zsh-hook
	add-zsh-hook chpwd _chpwd_devtodo

	_chpwd_devtodo ()
	{
		[[ -t 0 && -r .todo ]] && devtodo "$TODO_OPTIONS[@]"
	}
fi

# cdrdao
if [[ -x =cdrdao ]]
then
	cdrdao ()
	{
		local cmd=$1
		shift
		command cdrdao $cmd --device '/dev/cdrom' --driver generic-mmc:0x10 "$@"
	}
fi

# dosage
if [[ -x =dosage ]]
then
	dosage ()
	{
		if [[ -z $VIRTUAL_ENV ]]
		then
			command dosage -b ~/annex/webcomics -v --adult -o json "$@"
		else
			command dosage -vv --adult "$@"
		fi
	}
fi

if [[ -x =doas ]]
then
	doas ()
	{
		local -U path=($path /sbin /usr/sbin /usr/local/sbin)
		local WAYLAND_DISPLAY="$XDG_RUNTIME_DIR/$WAYLAND_DISPLAY"
		command doas "$@"
	}
fi

if [[ -x =git ]]
then
	autoload -Uz add-zsh-hook
	add-zsh-hook chpwd _chpwd_gitblame

	_chpwd_gitblame ()
	{
		if [[ -d .git && -f .git-blame-ignore-revs ]]
		then
			git config blame.ignoreRevsFile .git-blame-ignore-revs
		fi
	}
fi

if [[ -x =podman ]]
then
	# Funky hack to deal with pam_tmpdir
	podman ()
	{
		(
			unset TMPDIR
			exec =podman "$@"
		)
	}
fi

# mise-en-place
if [[ -x =mise ]]
then
	eval "$(mise activate zsh)"
	# TAB completion needs the "usage" tool (install with "mise use -g usage")
	[[ -x =usage ]] && _cache-completion =mise "mise completion zsh"
fi

# PostgreSQL XDG redirects
if [[ -x =psql ]]
then
	export PSQLRC="${XDG_CONFIG_HOME:-$HOME/.config}/pg/psqlrc"
	export PSQL_HISTORY="${XDG_CACHE_HOME:-$HOME/.cache}/psql_history"
	export PGPASSFILE="${XDG_CONFIG_HOME:-$HOME/.config}/pg/pgpass"
	export PGSERVICEFILE="${XDG_CONFIG_HOME:-$HOME/.config}/pg/pg_service.conf"
fi

# local VNC stuff
[[ -x =vncserver ]] && alias vnc-run='vncserver -geometry 1200x600 -depth 15 -localhost'
[[ -x =xvncviewer ]] && alias vnc-view='xvncviewer -passwd ~/.vnc/passwd :1'

# httrack
if [[ -e /usr/lib/httrack/htsserver ]]
then
	alias hts='s /usr/lib/httrack/htsserver /usr/share/httrack/'
	alias htsdiff='vimdiff <(sort -u old.lst) <(sort -u new.lst)'
fi

# distcc
export DISTCC_HOSTS="localhost"
case $HOST in
	(may) DISTCC_HOSTS="kos-mos.local $DISTCC_HOSTS";;
esac

# one-shots
[[ -x =pstree ]] && alias pstree='pstree -u'  # show users in pstree
alias mv='mv -i'  # mv should ask...
alias leech='wget -r -np -l inf -N -kK -E'  # Download a full page...

export MINICOM='-mc on'  # MiniCom with colors
export EMAIL='tobias-debian@23.gs'  # mail address for Debian tools
export RI='-f ansi'
export EDITOR="vim"
export DI_ARGS="-x tmpfs,devtmpfs,ntfs -h"  # statfs64 is slooooooow on ntfs...
#export _JAVA_AWT_WM_NONREPARENTING=1  # Because Java is a piece of shit
export WEBPROCESS_HOME=~/doc/dev/webprocess
export LIBBLURAY_CP="/usr/share/java/libbluray.jar"
export DVDCSS_CACHE="${XDG_CACHE_HOME:-$HOME/.cache}"/dvdcss
export SQLITE_HISTORY="${XDG_CACHE_HOME:-$HOME/.cache}"/sqlite_history
export MOSH_ESCAPE_KEY="~"
export XZ_DEFAULTS="-T0"

if [[ $WSL_INTEROP ]]
then
	export BROWSER="powershell.exe /C start"
fi

# Windows Terminal is true-color capable, but doesn't announce it
# See https://github.com/microsoft/terminal/issues/11057
if [[ $WT_SESSION ]]
then
	export COLORTERM=truecolor
fi

if [ -e ~/doc/secret/envs ]
then
	. ~/doc/secret/envs
fi

# vi: set ts=4 sw=4 ft=zsh:
