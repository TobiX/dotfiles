# X11 related functions

# This checks if a specific display is availible, covered are VNC, every X11
# that binds TCP sockets, XFree86, Xorg and maybe others... (modeled afer the
# functions in VNC4)
isdisplayfree () {
	local d="$1" p f
	zmodload -i zsh/net/tcp

	for p in 6000 5900
	do
		ztcp -l $(($p + $d)) 2>/dev/null || return 1
		ztcp -c
	done

	for f in /tmp/.X$d-lock /tmp/.X11-unix/X$d /usr/spool/sockets/X11/$d
	do
		[[ -e $f ]] && return 1
	done

	return 0
}

# Get the next free X Display
getfreedisplay () {
	for i in {0..99}
	do
		isdisplayfree $i && return $i
	done
	return 255
}

wineprefixes="${XDG_DATA_HOME:-$HOME/.local/share}/wineprefixes"
export WINEPREFIX="$wineprefixes/default"

[[ -d "$wineprefixes/diablo2" ]] && diablo2 () {
	local i lang="${1-nochange}"
	local d2="${XDG_DATA_HOME:-$HOME/.local/share}/wineprefixes/diablo2"
	setopt local_options null_glob
	pushd "$d2/drive_c/Program Files/Diablo II" > /dev/null
	for i in *.$lang; do ln -sf $i ${i:r}; done
	WINEPREFIX="$d2" wine Game.exe -w -direct -d3d
	popd > /dev/null
}

unset wineprefixes

xinabox () {
	getfreedisplay
	local display=$?
	if [[ $display != 255 ]]
	then
		Xephyr :$display -screen 1024x768 &
		sleep 1
		DISPLAY=:$display openbox &
#		xkbcomp $DISPLAY :$display
	fi
}

true

# vi: set ft=zsh ts=4 sw=4:
