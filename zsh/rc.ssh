# SSH magic

# Link to existing SSH agent
sshlink="$HOME/.cache/newest-ssh-agent"

if [[ -o login && $SSH_AUTH_SOCK != "" ]]
then
	ln -sf "$SSH_AUTH_SOCK" "$sshlink"
fi

[[ -e $sshlink ]] && export SSH_AUTH_SOCK="$sshlink"

unset sshlink

# WSL2 to PuTTY agent
if [[ $SSH_AUTH_SOCK == "" && $WSL_INTEROP != "" && -x =npiperelay.exe ]]
then
	export SSH_AUTH_SOCK="$HOME/.cache/wsl2-ssh-agent"
	if ! ss -a 2>&1 | grep -q "$SSH_AUTH_SOCK"
	then
		rm -f "$SSH_AUTH_SOCK"
		pa_sock="$(powershell.exe 'Write-Host (Get-ChildItem \\.\pipe\ | Where-Object { $_.FullName -like "*pageant*" }).Name')"
		(setsid nohup socat UNIX-LISTEN:$SSH_AUTH_SOCK,fork EXEC:"npiperelay.exe //./pipe/$pa_sock" >/dev/null 2>&1 &)
		unset pa_sock
	fi
fi

# WSL/CygWin
if [[ $SSH_AUTH_SOCK == "" ]]
then
	# Try locations for
	# - https://github.com/benpye/wsl-ssh-pageant
	# - CygWin-KeyAgent integration
	for sock in /mnt/c/Tools/ssh-agent.sock /home/keeagent.sock
	do
		if [[ -e $sock ]]
		then
			export SSH_AUTH_SOCK="$sock"
			break
		fi
	done
fi

true
# vi: set ft=zsh ts=4 sw=4:
