# Stuff to detect tmux/screen and move some variables from outside to the inside

# tmux already copies some variables when re-attaching, we only need
# to ask nicely for them
if [[ -x =tmux ]]
then
	if [[ -n $TMUX ]]
	then
		functions[precmd]="${functions[precmd]//tmux_magic_precmd}
			tmux_magic_precmd"

		tmux_magic_precmd () {
			eval $(tmux show-environment -s)
		}

	fi
fi

if [[ -x =screen ]]
then
	alias s='screen'

	if [[ -n $STY ]]
	then
		# Now for the bigger magic
		functions[precmd]="${functions[precmd]//screen_magic_precmd}
			screen_magic_precmd"

		screen_magic_precmd () {
			local parent=$(_get_screen_parent)
			if [[ $parent != $screen_magic_lastparent && -r /proc/$parent/environ ]]
			then
				# Do real magic!
				eval $(grep -z '^\(SSH_[A-Z_]*\|DISPLAY\)=' /proc/$parent/environ |
					tr '\0' '\n' | sed 's/^/export /; s/=/="/; s/$/"/')
				screen_magic_lastparent=$parent
			fi
		}
	fi
fi

# vi: set ts=4 sw=4 ft=zsh:
