# Some nice mounting stuff

# remount root as ro or rw (embedded)
[[ -x /sbin/init-ro ]] && root () {
	if mount | grep -q " on / type .*ro"
	then
		sudo mount -o remount,rw / && echo "root $bg_bold[red]read-write$reset_color"
	else
		sudo mount -o remount,ro / && echo "root $bg_bold[green]read-only$reset_color"
	fi
}

# mount encfs .<param> onto <param>
[[ -x =encfs ]] && enc () {
	local mp="$1" dir="$1:h/.$1:t"

	if [[ ! -d $dir ]]; then
		echo "Error: directory '$dir' does not exist!"
		return 1
	fi
	dir=`cd $dir; pwd`

	if [[ ! -d $mp ]]; then
		mkdir -p $mp
	fi
	mp=`cd $mp; pwd`

	_mount_or_umount "$mp" "encfs $dir $mp" "fusermount -u $mp"
}

if [[ -f ~/.ssh/known_hosts ]]
then
	grep -q '^kurumi' ~/.ssh/known_hosts && {
		alias kurumi-home="_mount_or_umount_sshfs kurumi:.. $HOME/mnt"
		alias kurumi-media="_mount_or_umount_sshfs kurumi:/media $HOME/mnt"
	}
	grep -q '^hikaru' ~/.ssh/known_hosts && {
		alias hikaru-home="_mount_or_umount_sshfs hikaru:.. $HOME/mnt"
	}
fi

true
# vim: set ts=4 sw=4 ft=zsh:
