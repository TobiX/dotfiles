# Some handy settings for different *PATH arrays

# make INFOPATH and CLASSPATH special like the other PATHs
typeset -gxT INFOPATH infopath
typeset -gxT CLASSPATH classpath
# automatically remove duplicates from these arrays
typeset -U path cdpath fpath manpath infopath

# add some handy path constructs
for d in \
	~/.local/bin \
	~/pkg/bin \
	${XDG_DATA_HOME:-$HOME/.local/share}/cargo/bin \
	~/doc/dev/scripts \
	$DOTFILES_BASE/bin \
	/usr/games \
	/var/lib/gems/*/bin \
	~/.yarn/bin \
	/var/lib/flatpak/exports/bin
do
	[[ -d $d ]] && path=($d $path)
done

for d in \
	$DOTFILES_BASE/zsh/funcs \
	~/doc/dev/zfuncs \
	"${XDG_CACHE_HOME:-$HOME/.cache}/zsh/funcs" \
	/usr/lib/python3/dist-packages/argcomplete/bash_completion.d
do
	if [[ -d $d ]]
	then
		fpath=($d $fpath)
		autoload -Uz $d/*(.:t)
	fi
done

if [[ -d ~/src ]]
then
	export GOPATH=$HOME/src/go
	[[ -d $GOPATH/bin ]] && path+=$GOPATH/bin
fi

export VITASDK=$HOME/src/vitasdk
if [[ -d $VITASDK ]]
then
	path=($path $VITASDK/bin)
else
	unset VITASDK
fi

# cdpath is strange...
#for d in ~ ~/src ~/doc /work/tobias
#do
#	[[ -d $d ]] && cdpath=($d $cdpath)
#done

for d in ~/info
do
	[[ -d $d ]] && infopath=($d $infopath)
done

classpath=(.)
for d in /usr/share/java/antlr3.jar /usr/share/java/stringtemplate.jar
do
	[[ -f $d ]] && classpath=($d $classpath)
done
export CLASSPATH

# we don't want anything there, really
unset MANPATH
unset d

# vim: set ts=4 sw=4 ft=zsh:
